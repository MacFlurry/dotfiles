##
##  Completion
##

# context string: ':completion:[function]:[completer]:[command]:[arg]' [tag]

zstyle ':completion::complete:*' use-cache on
zstyle ':completion::complete:*' cache-path $HOME/.zsh/cache/$HOST

# Finds the correct version of dircolors with appropriate arguments.
dircolors_string()
{
    local cmd=""
    local color_file="$HOME/.dir_colors"
    local dc=$(which dircolors 2> /dev/null)
    local gdc=$(which gdircolors 2> /dev/null)

    if [[ -x $dc ]]; then
        cmd=$dc 
    elif [[ -x $gdc ]]; then
        cmd=$gdc
    fi


    local file=""
    if [[ -n $cmd ]]; then
        if [[ -f $color_file ]]; then 
            file=$color_file

            # The file suffix for a second ~/.dir_colors file supporting newer
            # commands.
            local suffix='modern'

            # Newer versions support more keywords.
            local version=${${${(f)"$($dc --version)"}[1]}[(w)-1]}
            [[ $version > 6.0 && -f "$color_file.$suffix" ]] && file+=".$suffix"
        fi

        eval $($cmd -b $file)
    else
        # TODO. No (g)dircolors available.
        
    fi

}

dircolors_string
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

# Match uppercase from lowercase.
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# List of completers to use.
zstyle ':completion:*::::' completer _expand _complete _ignored _approximate

# Prevent CVS files/directories from being completed.
zstyle ':completion:*:(all-|)files' ignored-patterns '(|*/)CVS'
zstyle ':completion:*:cd:*' ignored-patterns '(*/)#CVS'
# Ignore completion functions for unknown commands.
zstyle ':completion:*:functions' ignored-patterns '(_*|prec(cmd|exec))'

# Process completion
#zstyle ':completion:*:*:*:*:processes' menu yes select
#zstyle ':completion:*:*:*:*:processes' force-list always
zstyle ':completion:*:processes-names' command 'ps -e -o comm='
zstyle ':completion:*:processes' command 'ps -aU$USER'
zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;32'

# SSH 
if [[ -f ${HOME}/.ssh/known_hosts ]] ; then
  zstyle ':completion:*' hosts ${${${(f)"$(<$HOME/.ssh/known_hosts)"}%%\ *}%%,*}
fi
zstyle ':completion:*:scp:*' tag-order \
    'hosts:-host hosts:-domain:domain hosts:-ipaddr:IP\ address *'
zstyle ':completion:*:scp:*' group-order \
    users files all-files hosts-domain hosts-host hosts-ipaddr
zstyle ':completion:*:ssh:*' tag-order \
    users 'hosts:-host hosts:-domain:domain hosts:-ipaddr:IP\ address *'
zstyle ':completion:*:ssh:*' group-order \
    hosts-domain hosts-host users hosts-ipaddr
zstyle ':completion:*:(ssh|scp):*:hosts-host' ignored-patterns \
    '*.*' loopback localhost
zstyle ':completion:*:(ssh|scp):*:hosts-ipaddr' ignored-patterns \
    '^<->.<->.<->.<->' '127.0.0.<->'
zstyle ':completion:*:(ssh|scp):*:users' ignored-patterns \
    '_*' adm bin daemon halt lp named shutdown sync 

# vim: ft=zsh
