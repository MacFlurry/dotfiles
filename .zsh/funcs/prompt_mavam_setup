# Adapated version of Bart Sch√§fer's prompt.
zmodload -i zsh/parameter || return 1

prompt_mavam_help () {
    setopt localoptions nocshnullcmd noshnullcmd
    [[ $ZSH_VERSION < 4.2.2 ]] &&
        print 'Requires ZSH_VERSION 4.2.2'$'\n'
    <<-\EOF
        This prompt gives the effect of left and right prompts on the upper
        line of a two-line prompt. 

            prompt mavam [on|off] [color...]

        You may provide up to five colors, though only three colors (red,
        blue, and the default foreground) are used if no arguments are
        given.  The defaults look best on a light background.

        The "off" token temporarily disables the theme; "on" restores it.
        No background colors or hardwired cursor motion escapes are used,
        and it is not necessary to setopt promptsubst.
EOF
    [[ $(read -sek 1 "?${(%):-%S[press return]%s}") == [Qq] ]] &&
        print -nP '\r%E' && return
        <<-\EOF

        The "upper left prompt" looks like:
            user@machine :: /current/working/dir
        The first three color arguments apply to these components.  

        The "upper right prompt" looks like:
            [exit code] date time
        The fourth color is used for the date, and the fifth for the time.
        The exit code is only displayed if non-zero. As with RPS1, first the
        date and then the time disappear as the upper left prompt grows too
        wide.  The clock is not live; it changes only after each command, as
        does the rest of the prompt.
EOF
    [[ $(read -sek 1 "?${(%):-%S[press return]%s}") == [Qq] ]] &&
        print -nP '\r%E' && return
        <<-\EOF

        When RPS1 (RPROMPT) is set before this prompt is selected and a
        fifth color is specified, that color is turned on before RPS1 is
        displayed and reset after it.  Other color changes within RPS1, if
        any, remain in effect.

        This prompt hijacks psvar[7] through [9] to avoid having to reset
        the entire PS1 string on every command.  It uses TRAPWINCH to set
        the position of the upper right prompt on a window resize, so the
        prompt may not match the window width until the next command.

        When setopt nopromptcr is in effect, an ANSI terminal protocol
        exchange is attempted in order to determine the current cursor
        column, and the width of the upper prompt is adjusted accordingly.
        If your terminal is not ANSI compliant, this may cause unexpected
        behavior, and in any case it may consume typeahead.  (Recommended
        workaround is to setopt promptcr.)
EOF
    [[ $(read -sek 1 "?${(%):-%S[done]%s}") == $'\n' ]] ||
        print -nP '\n%E'
}

integer PSCOL=1

prompt_mavam_precmd () {
    setopt localoptions noxtrace noksharrays unset
    local escape colno lineno

    # Also removes the 4 special characters, so we have to add them later.
    local remove='(%([BSUbfksu]|[FB]{*})|$(fg|ch)\[*[a-z]\])' 

    # Using psvar here protects against unwanted promptsubst expansions.
    psvar[9]=''

    # Reset the truncation widths for upcoming computations
    ((PSCOL == 1)) || { PSCOL=1 ; prompt_mavam_ps1 }
    if [[ -o promptcr ]]
    then
        # Emulate the 4.3.x promptsp option if it isn't available
        eval '[[ -o promptsp ]] 2>/dev/null' ||
            print -nP "${(l.COLUMNS.. .)}\e[s${(pl.COLUMNS..\b.)}%E\e[u" >$TTY
    else IFS='[;' read -s -d R escape\?$'\e[6n' lineno PSCOL <$TTY
    fi
    ((PSCOL == 1)) || prompt_mavam_ps1
    ((colno = COLUMNS-PSCOL))

    # Compute and set the padding between upper left and right prompts.
    if (((colno -= ${#${(f)${(%%)${(S)PS1//$~remove/}}}[1]} + 4) > 0)); then
        # FIXME: instead of 'q', the padding function should use $ch[hbar], but
        # my zsh magic is not elaborate enough to figure out why.
	psvar[9]="${(l:colno::q:)}"
    fi
}

prompt_mavam_ps1 () {
    setopt localoptions noxtrace noksharrays

    # Set up the components.
    local -h host dir ec colno fillbar date time indicator rs="%f%k%u"
    host="%(!.%{$fg[root]%}%m.%{$fg[user]%}%n%{$fg[at]%}@%{$fg[host]%}%m)$rs"
    dir="%{$fg[dir]%}%8~$rs"
    ec="%(?..%{$fg[red]%}%U%?$rs)"
    fillbar='$ch[on]%9v$ch[off]'
    date="%{$fg[date]%}%D{%H:%M:%S %y-%m-%d}"
    indicator="%{$fg[ch]%}(%{$fg[indicator]%}%#%{$fg[ch]%})$rs"

    # This is just a tad silly ...
    [[ $prompt_theme[1] = oliver ]] && PS1='[%h%0(?..:%?)]%# ' ||
        PS1=${PS1//$prompt_newline/$'\n'}

    # Assemble the left prompt.
    local -ah prompt
    prompt=(
        "%$[COLUMNS-PSCOL]>..>"  # Begin truncation (upper left prompt)
        '$ch[charset]$fg[ch]$ch[on]$ch[ul]$ch[hbar]$ch[off]'
        "("
        "$host"
        "$fg[ch])"
        "%<<"                    # End truncation (end upper left prompt)
        "$fillbar"               # Pad line to upper right position
        "$fg[ch]("
        "$dir"
        "$fg[ch])"
        '$fg[ch]$ch[on]$ch[hbar]$ch[ur]$ch[off]'
        $'\n'
        '$fg[ch]$ch[on]$ch[ll]$ch[hbar]$ch[off]'
        "$indicator "
        )
    PROMPT="${(j::)prompt}"

    # Assemble the right prompt.
    local -ah rprompt
    rprompt=(
        "$ec "
        "$fg[ch]("
        "$date"
        "$fg[ch])"
        '$ch[on]$ch[hbar]$ch[lr]$ch[off]'
        )
    RPROMPT="${(j::)rprompt}"
}

prompt_mavam_winch () {
    setopt localoptions nolocaltraps noksharrays unset

    # Delete ourself from TRAPWINCH if not using our precmd insert.
    [[ $precmd_functions = *prompt_mavam_precmd* ]] && prompt_mavam_ps1 ||
        functions[TRAPWINCH]="${functions[TRAPWINCH]//prompt_mavam_winch}"
}

prompt_mavam_setup () {
    setopt localoptions nolocaltraps noksharrays unset
    typeset -gA fg ch

    # A few extra niceties...
    repeat 1 case "$1:l" in
      (off|disable)
        add-zsh-hook -D precmd "prompt_*_precmd"
        functions[TRAPWINCH]="${functions[TRAPWINCH]//prompt_mavam_winch}"
        return 1
        ;;
      (on|enable)
        # Backup existing prompt.
        shift
        [[ $prompt_theme[1] = mavam ]] && break
        ;&
      (*)
        # Use the fg assoc to hold our selected colors ...
        # This used to be provided by the function colors, but is now
        # set directly from here.  There should be no clash if both
        # are in use.
        fg[root]="%F{red}"
        fg[ec]="%F{red}"
        fg[ch]="%F{${1:-blue}}"
        fg[user]="%F{${2:-cyan}}"
        fg[at]="%F{${3:-green}}"
        fg[host]="%F{${2:-cyan}}"
        fg[dir]="%F{${4:-cyan}}"
        fg[date]="%F{${5:-default}}"
        fg[time]="%F{${5:-default}}"

        if [[ -z "$SSH_CONNECTION" ]]; then
            fg[indicator]="%F{white}"   # Local prompt
        else
            fg[indicator]="%F{yellow}"  # Remote prompt
        fi

        # Use the ch assoc to hold special characters.
        local -Ah altchar
        set -A altchar ${(s..)terminfo[acsc]}
        ch[charset]="%{$terminfo[enacs]%}"
        ch[on]="%{$terminfo[smacs]%}"
        ch[off]="%{$terminfo[rmacs]%}"
        ch[hbar]=${altchar[q]:--}
        ch[ul]=${altchar[l]:--}
        ch[ll]=${altchar[m]:--}
        ch[lr]=${altchar[j]:--}
        ch[ur]=${altchar[k]:--}
        ;;
    esac

    prompt_mavam_ps1

    # No RPS1 by default because prompt_off_setup doesn't fix it.
    (($#RPS1 && $# > 4)) && RPS1="%F{$5}$RPS1%f"

    # Paste our special commands into precmd and TRAPWINCH
    add-zsh-hook precmd prompt_mavam_precmd
    functions[TRAPWINCH]="${functions[TRAPWINCH]//prompt_mavam_winch}
        prompt_mavam_winch"

    return 0
}

prompt_mavam_preview () {
    prompt_preview_theme mavam "$@"
}

[[ -o kshautoload ]] || prompt_mavam_setup "$@"
