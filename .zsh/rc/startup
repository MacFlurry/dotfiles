##
## Startup
##

# Setup SSH agent environment. But don't start agent(s) as root or remotely.
if [[ $UID != 0 && -z "$SSH_CONNECTION" ]]; then
    # Trust only $HOME/bin for keychain.
    local kc_bin=$HOME/bin/keychain 
    if [[ -x $kc_bin ]]; then
        local identities=$HOME/.ssh/identities
        if [[ -n $identities/*~*pub ]]; then
            $kc_bin -q --host fixed $identities/*~*pub
            source ~/.keychain/fixed-sh
        fi
    fi
fi

# Report about the following processes.
procs=( {ssh,gpg}-agent )
for p in $procs ; do
	local -a pids
	pids=($(findpids $p))
	if (( $#pids )) ; then
		printf "%d %s running: [%s]\n" $#pids "${p}(s)" "$pids"
	fi
done

# Notify if there is a detached screen.
if [[ -x $(whence -p screen) ]] ; then
	#screen_pids=($(screen -ls | awk -F. '/[ \t]+[0-9]+/ {print $1}'))
	screen_pids=(${${(M)${(f)"$(screen -ls)"}:#(#s)[[:space:]]##([0-9]##).*}/(#b)[[:space:]]#([0-9]##).*/$match[1]})
	if (( ${#screen_pids} )) ; then
		printf "%d screen(s) running: [%s]\n" $#screen_pids "${screen_pids}"
	fi
fi

# vim: ft=zsh
