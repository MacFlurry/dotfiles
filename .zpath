#
# zpath - Sets PATH related variables.
#

# Automatically remove duplicates from these arrays.
typeset -U path cdpath fpath manpath

# System $PATH.
path=( 
    /usr/local/bin
    /usr/bin
    /bin
    /usr/local/sbin
    /usr/sbin
    /sbin
    /usr/X11R6/bin      # X11
    /usr/ucb            # SunOS
    /usr/sww/bin        # SunOS
)

# Man pages.
manpath=(
    /usr/local/man
    /usr/man
)

# Function path.
fpath=(~/.zsh/completions $fpath)

# Source OS specific zpath
if [[ -f $HOME/.zpath.${OS} ]]; then
    source $HOME/.zpath.${OS}
fi

# Home prefix has highest precedence, thus sourced last.
path=( ~/bin $path )
manpath=( ~/man $manpath )

# Removes non-existent directories from an array.
clean-path ()
{             
    local element
    local build
    build=()
    # Make sure that this works even with variables containing IFS characters,
    # if you're crazy enough to setopt shwordsplit.
    eval '
    foreach element in "$'"$1"'[@]"; do
        [[ -d "$element" ]] && build=("$build[@]" "$element")
    done
    '"$1"'=( "$build[@]" )
    '
}

# Finally, clean the path variables.
clean-path path
clean-path manpath
clean-path fpath

# vim: ft=zsh
