##
##  Prompt
## 

# Legacy prompt for machines that with Zsh version < 4.3.7
function old_prompt()
{ 
  local none=$'%{\e[0m%}'
  local black=$'%{\e[0;30m%}'
  local red=$'%{\e[0;31m%}'
  local green=$'%{\e[0;32m%}'
  local yellow=$'%{\e[0;33m%}'
  local blue=$'%{\e[0;34m%}'
  local purple=$'%{\e[0;35m%}'
  local cyan=$'%{\e[0;36m%}'
  local white=$'%{\e[0;37m%}'
  local blackB=$'%{\e[1;30m%}'
  local redB=$'%{\e[1;31m%}'
  local greenB=$'%{\e[1;32m%}'
  local yellowB=$'%{\e[1;33m%}'
  local blueB=$'%{\e[1;34m%}'
  local purpleB=$'%{\e[1;35m%}'
  local cyanB=$'%{\e[1;36m%}'
  local whiteB=$'%{\e[1;37m%}'

  local col1=${cyanB}   # user@host
  local col2=${blueB}   # % 
  local col3=${col1}    # [/path/to/foo]

  # Prompt
  if [[ -z "$SSH_CONNECTION" ]] ; then
    # Local prompt
    export PROMPT="%(!.${redB}%m.${col1}%n${greenB}@${col1}%m) ${col2}%# ${none}" 
  else
    # Remote prompt
    export PROMPT="%(!.${redB}%m ${yellow}+.${col1}%n${redB}@${col1}%m) ${col2}%# ${none}" 
  fi

  # Right-side prompt
  export RPROMPT="%(?..${red}%U%?%u${none}) ${col2}[%(!.${redB}.${col3})%35<..<%~${col2}]${none}"

  # $PS2 (used in loops, statements, ...)
  export PS2="%(4_:... :)%3_ ${col2}>${none} "   

  # Correction prompt
  export SPROMPT="error: correct '%B%R%b' to '%B%r%b'? (%Un%uo|%Uy%ues|%Ua%ubort|%Ue%udit) " 
}

# Git integration
# ===============
# Update the vcs_info_msg_ magic variables, but only as little as possible.
#
# This variable dictates weather we are going to do the git prompt update
# before printing the next prompt. This can save up to 10 secs of work.
PR_GIT_UPDATE=1

# Called before command excution: update the git prompt if the command involved
# the word 'git'.
function zsh_git_prompt_preexec 
{
  case "$(history $HISTCMD)" in 
    *git*)
      PR_GIT_UPDATE=1
      ;;
  esac
}

# Called after directory change: assume that we have to update the git prompt.
function zsh_git_prompt_chpwd 
{
  PR_GIT_UPDATE=1
}

# Called before prompt generation. If needed, update the prompt info.
function zsh_git_prompt_precmd 
{
  if [[ -n "$PR_GIT_UPDATE" ]]
  then
    vcs_info 'prompt'
    PR_GIT_UPDATE=
  fi
}

# Prompt initialization
# =====================
if is-at-least 4.3.7
then
  add-zsh-hook preexec zsh_git_prompt_preexec
  add-zsh-hook chpwd zsh_git_prompt_chpwd
  add-zsh-hook precmd zsh_git_prompt_precmd
  promptinit
  setopt prompt_subst
  # See ~/.zsh/funcs/prompt_mavam_setup for details.
  prompt mavam
  #prompt mavam blue cyan green green yellow
else
  old_prompt    
fi

# vim:ft=zsh
