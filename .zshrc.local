# 
#  Local Zsh configuration 
#

# Setup SSH and GPG agents. But don't start them as root or remotely.
if [[ $UID != 0 && -z "$SSH_CONNECTION" ]]; then
    if which keychain &> /dev/null; then
        keychain -q --host fixed $HOME/.ssh/identities/*~*pub 9C8D4B41
        [[ -f ~/.keychain/fixed-sh ]] && source ~/.keychain/fixed-sh

        export GPG_TTY=$(TTY)
        [[ -f ~/.keychain/fixed-sh-gpg ]] && source ~/.keychain/fixed-sh-gpg
    fi
fi

# Sets the $BROPATH environment variable
function bro_path
{
    export BROPATH="$1:$1/sigs:$1/site:$1/time-machine"
}

# Set up a Bro source tree.
function bro_source
{ 
    path=( $1/src $path )
    bro_path "$1/policy"
}

# Set up a prefix where Bro is installed.
function bro_prefix
{
    path=( $1/bin $path )
    bro_path "$1/share/bro"
}

# Autmatically setup Bro.
# $1: array of prefixes
# $2: array of Bro source trees.
function bro_auto_setup
{
    local -a prefixes trees; 
    [[ $1 != "" ]] && prefixes=$1 || prefixes=( ~/code/root /usr /usr/local )
    [[ $2 != "" ]] && trees=$2 || trees=( ~/code/bro/bro ~/src/bro )

    # First check the source trees.
    foreach dir ($trees)
    {
        # FIXME: The default build directory is different now that Bro moved to
        # CMake.
        if [[ -x $dir/src/bro ]] ; then
            bro_source $dir 
            return
        fi
    }

    # Then check the prefixes.
    foreach pfx ($prefixes)
    {
        if [[ -x $pfx/bin/bro ]]
        then
            bro_prefix $pfx 
            return
        fi
    }
}

bro_auto_setup

# vim: ft=zsh 
